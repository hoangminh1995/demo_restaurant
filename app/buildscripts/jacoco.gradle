apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.1"
}

project.afterEvaluate {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            task "${testTaskName}Coverage"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

                def excludes = [
                        '**/R.class',
                        '**/R$*.class',
                        '**/Manifest*.*',
                        'android/**/*.*',
                        '**/BuildConfig.*',
                        '**/*$ViewBinder*.*',
                        '**/*$ViewInjector*.*',
                        '**/Lambda$*.class',
                        '**/Lambda.class',
                        '**/*Lambda.class',
                        '**/*Lambda*.class']

                def javaClasses = fileTree(
                        dir: "${project.buildDir}/intermediates/classes/${sourcePath}",
                        excludes: excludes
                )

                def kotlinClasses = fileTree(
                        dir: "${project.buildDir}/tmp/kotlin-classes/${sourceName}",
                        excludes: excludes
                )

                classDirectories.from = files([javaClasses], [kotlinClasses]
                ).filter({ file ->
                    file.name.contains('BaseViewModel') ||
                            file.name.contains('BaseAuthViewModel') ||
                            file.name.contains('LoginViewModel') ||
                            // send event
                            file.name.contains('BaseSendEventViewModel') ||
                            file.name.contains('MainViewModel') ||
                            file.name.contains('DepartureViewModel') ||
                            file.name.contains('ArrivalViewModel') ||
                            file.name.contains('PortActivityViewModel') ||
                            file.name.contains('OffPortLimitViewModel') ||
                            file.name.contains('DropAnchorViewModel') ||
                            file.name.contains('StartDriftingViewModel') ||
                            file.name.contains('FirstPilotViewModel') ||
                            file.name.contains('SelectPurposeViewModel') ||
                            file.name.contains('AnchorAweighViewModel') ||
                            file.name.contains('CompleteDriftingViewModel') ||
                            file.name.contains('SelectPurposeViewModel') ||
                            file.name.contains('SelectPortViewModel') ||
                            // survey
                            file.name.contains('BaseSurveyViewModel') ||
                            file.name.contains('ListSurveyViewModel') ||
                            file.name.contains('ListSurveyQuestionViewModel')
                })

                def coverageSourceDirs = [
                        "src/main/java",
                        "src/$productFlavorName/java",
                        "src/$buildTypeName/java"
                ]

                additionalSourceDirs.from = files(coverageSourceDirs)
                sourceDirectories.from = files(coverageSourceDirs)
                executionData.from = files("${project.buildDir}/jacoco/${testTaskName}.exec")

                reports {
                    xml.enabled = true
                    html.enabled = true
                }
            }
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}
